// Main Script
$MODULES = "Lights textures_sprite_animation textures_background_scrolling Eyes";
$MODULES = $MODULES SPC "textures_mouse_painting gridtest";
$MODULES = $MODULES SPC "RenderMap BatchTest";
$MODULES = $MODULES SPC "DeltaTime shapes_starfield_nativ";
// $MODULES = $MODULES SPC "looptest blank shapes_starfield_effect shapes_starfield_batch";

// LOADFAILTEST shapes_starfield_effect_v1

function Main::init(%this) {
    SetConfigFlags(%this.flags );

    InitWindow(%this.screenWidth, %this.screenHeight, %this.caption);
    SetTargetFPS(%this.currentFps);

    // Image/Icon Test
    // LoadImage return a Image identifier (imageId)
    %this.iconImg = LoadImage("assets/texture/raylib_32x32.png");
    SetWindowIcon(%this.iconImg);
    // IconImg can be unloaded now, but i keep it for later testing texture.
    // It's unloaded when programm ends automaticly
    //  UnloadImage(%this.iconImg);

    // SetExitKey(0); // i want to use escape to unload module and display a module list
    %this.loadModule();
}

function Main::loadModule(%this, %setNewModuleIndex) {
    if (%setNewModuleIndex !$= "" ) {
        %this.moduleIndex = %setNewModuleIndex;
    }
    %cnt = getWordCount($MODULES);
    if (%this.moduleIndex >= %cnt)  %this.moduleIndex = 0;
    if (%this.moduleIndex < 0 ) %this.moduleIndex = %cnt - 1;

    %this.moduleName = getword($MODULES, %this.moduleIndex);

    if (!exec("assets/modules/" @ %this.moduleName @ ".cs")) {
        error("Failed to load module" SPC %this.moduleName);
        return false;
    }

    if (isObject(%this.module)) {
        %this.module.delete();
    }
    SetTargetFPS(%this.currentFps); //reset fps
    %cmd = %this.getId() @ ".module = Create" @ %this.moduleName @ "();";
    echo("loadModule command is:" SPC %cmd);
    eval(%cmd);
    %created = isObject(%this.module);
    echo("loadModule success:" SPC %created);
    if (!%created || !%this.module.isMethod("render")) {
        error("Module failed!! obj:" SPC %this.module SPC "method render exists:" SPC %this.module.isMethod("render"));
        if (%created) %this.module.delete();
        %this.module = 0;
        return false;
    }
    return true;
}

function Main::loop(%this) {

    if ( IsKeyDown(KEY_LEFT_CONTROL) || IsKeyDown(KEY_RIGHT_CONTROL) ) {
        if (IsKeyPressed(KEY_UP)) {
          %this.moduleIndex--;
          %this.loadModule();
        } else  if (IsKeyPressed(KEY_DOWN)) {
            %this.moduleIndex++;
            %this.loadModule();
        } else if (IsKeyPressed(KEY_R)) {
            rl();
        }
    }

    BeginDrawing();

    // DrawFPS(10, 10); //2500 fps without the module stuff below. Also > 2.4k  with

    if (isObject(%this.module)){
        %this.module.render();
        DrawText("Loaded module:" SPC %this.moduleName , 5, GetScreenHeight() - 30, 20, BLUE, true, BLACK);
    } else {
        ClearBackground("20 100 100");
        DrawText("Failed to load module:" SPC %this.moduleName, 5, GetScreenHeight() - 30, 20, RED);
    }
    DrawText("CTRL + UP or DOWN to load an other module. FPS:" SPC GetFPS()  , 5, GetScreenHeight() - 50, 20, BLACK, true, DARKGRAY);
    EndDrawing();
}

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
function MainInit() {

    $Main = new ScriptObject() {
        class = "Main";
        // dynamic fields:
        TypeString caption = "raylib-elfScript Demo Loader";
        TypeS32 screenWidth = 800;
        TypeS32 screenHeight = 450;
        TypeS32 currentFps = 60;
        TypeS32 flags = FLAG_WINDOW_RESIZABLE;
        TypeS32 moduleIndex = 0;
        TypeS32 module = 0;
    };


    $Main.init();


    return true;
}
function MainShutDown() {
    $Main.delete();
    CloseWindow();        // Close window and OpenGL context
}

function MainLoop() {
    $Main.loop();
}

function rl() { exec("assets/main.cs");}
